plugins {
    id 'org.jetbrains.kotlin.jvm' version "2.2.21"
    id 'org.jetbrains.kotlin.kapt' version "2.2.21"
    id "org.jetbrains.kotlin.plugin.allopen" version "2.2.21"
    id "org.jetbrains.kotlin.plugin.noarg" version "2.2.21"
    id 'io.quarkus'
    id 'io.ebean' version '17.2.0'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-rest'

    // Ebean ORM
    implementation 'io.ebean:ebean:17.2.0'
    implementation 'io.ebean:ebean-sqlite:17.2.0'
    implementation 'io.ebean:ebean-ddl-generator:17.2.0'
    implementation 'io.ebean:ebean-migration:13.6.0'
    kapt 'io.ebean:kotlin-querybean-generator:17.2.0'
    testImplementation 'io.ebean:ebean-test:17.2.0'

    // SQLite JDBC driver
    implementation 'org.xerial:sqlite-jdbc:3.47.2.0'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
}

group = 'fr.geoffreyCoulaud.pinryReborn'
version = '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    jvmArgs "--add-opens", "java.base/java.lang=ALL-UNNAMED"
}
allOpen {
    annotation("jakarta.ws.rs.Path")
    annotation("jakarta.enterprise.context.ApplicationScoped")
    annotation("jakarta.persistence.Entity")
    annotation("io.quarkus.test.junit.QuarkusTest")
    annotation("jakarta.persistence.MappedSuperclass")
    annotation("jakarta.persistence.Embeddable")
}

ebean {
    debugLevel = 1
}

noArg {
    annotation("jakarta.persistence.Entity")
    annotation("jakarta.persistence.MappedSuperclass")
    annotation("jakarta.persistence.Embeddable")
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_21
    kotlinOptions.javaParameters = true
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_21
}

tasks.register('generateDbMigration', JavaExec) {
    group = 'ebean'
    description = 'Generate Ebean database migration'
    mainClass = 'fr.geoffreyCoulaud.pinryReborn.migration.GenerateDbMigrationKt'
    classpath = sourceSets.test.runtimeClasspath
    dependsOn compileTestKotlin
}
